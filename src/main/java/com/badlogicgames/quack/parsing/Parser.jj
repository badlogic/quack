/* Copyright (c) 2006, Sun Microsystems, Inc.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     * Redistributions of source code must retain the above copyright notice,
 *       this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of the Sun Microsystems, Inc. nor the names of its
 *       contributors may be used to endorse or promote products derived from
 *       this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF
 * THE POSSIBILITY OF SUCH DAMAGE.
 */

/*
 * This file has been lifted from https://java.net/projects/javacc/sources/svn/show/trunk/examples/JavaGrammars/1.5?rev=689
 * and heavily modified to suite the needs of Quack. All praise be the Quack!
 */

options {
	STATIC = false;
	UNICODE_INPUT = true;
	JDK_VERSION = "1.5";
}
PARSER_BEGIN(Parser)
package com.badlogicgames.quack.parsing;

public class Parser {
}
PARSER_END(Parser)

//===========================================================================
// LEXER
//===========================================================================
// Comments & Whitespace
SKIP: { " " | "\t" | "\n" | "\r" | "\f" }

MORE: {
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT |
  "/*" : IN_MULTI_LINE_COMMENT
}

SPECIAL_TOKEN: {
  <SINGLE_LINE_COMMENT: "//" (~["\n", "\r"])* ("\n" | "\r" | "\r\n")?>
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN: {
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN: {
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE: {
  < ~[] >
}

// Keywords
TOKEN: {
	< IMPORT: "import" > |
	< MODULE: "module" > |
	< DEF: "def" > |
	< VAL: "val" > |
	< VAR: "var" > |	
	< STRUCT: "struct" > |
	< IF: "if" > |
	< ELIF: "elif" > |
	< ELSE: "else" > |
	< DO: "do" > |
	< WHILE: "while" > |
	< FOR: "for" > |
	< SIZEOF: "sizeof" >
}

// Inbuild Types
TOKEN: {
	< BOOL: "bool" > |

	< U8: "u8" > |
	< U16: "u16" > |
	< U32: "u32" > |
	< U64: "u64" > |
	
	< I8: "i8" > |
	< I16: "i16" > |
	< I32: "i32" > |
	< I64: "i64" > |
	
	< F32: "f32" > |
	< F64: "f64" >
}

// Literals
TOKEN : {
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL>
      | <HEX_LITERAL> 
      | <OCTAL_LITERAL>
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        <DECIMAL_FLOATING_POINT_LITERAL>
      | <HEXADECIMAL_FLOATING_POINT_LITERAL>
  >
|
  < #DECIMAL_FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<DECIMAL_EXPONENT>)?
      | "." (["0"-"9"])+ (<DECIMAL_EXPONENT>)?
      | (["0"-"9"])+ <DECIMAL_EXPONENT>
      | (["0"-"9"])+ (<DECIMAL_EXPONENT>)?
  >
|
  < #DECIMAL_EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < #HEXADECIMAL_FLOATING_POINT_LITERAL:
        "0" ["x", "X"] (["0"-"9","a"-"f","A"-"F"])+ (".")? <HEXADECIMAL_EXPONENT>
      | "0" ["x", "X"] (["0"-"9","a"-"f","A"-"F"])* "." (["0"-"9","a"-"f","A"-"F"])+ <HEXADECIMAL_EXPONENT>
  >
|
  < #HEXADECIMAL_EXPONENT: ["p","P"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

/* Identifiers */
TOKEN : {
  < IDENTIFIER: <LETTER> (<PART_LETTER>)* >
|
  < #LETTER:
      [  // all chars for which Character.isIdentifierStart is true         
         "A"-"Z",
         "_",
         "a"-"z"
      ]
  >
|
  < #PART_LETTER:
      [  // all chars for which Character.isIdentifierPart is true         
         "0"-"9",
         "A"-"Z",
         "_",
         "a"-"z"
      ]
  >
}

// Separators
TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < AT: "@" >
}

// Operators
TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < RSHIFT: ">>" >
| < ELLIPSIS: "..." >
}

//===========================================================================
// Parser
//===========================================================================
void CompilationUnit():
{}
{
	ModuleDeclaration()
	(ImportDeclaration())*	
	(StructDefinition() | FunctionDefinition())* 
}

void ModuleName():
{}
{
	<IDENTIFIER> ( "." <IDENTIFIER>)*
}

void ModuleDeclaration():
{}
{
	"module" ModuleName()
}

void ImportDeclaration():
{}
{
	"import" ModuleName()
}

void StructDefinition():
{}
{
	"struct" <IDENTIFIER> "{" 
		(FieldDeclaration())*
		(FunctionDefinition())*
	"}"		
}

void FieldDeclaration():
{}
{
	"val" <IDENTIFIER> ":" Type() |
	"var" <IDENTIFIER> ":" Type()
}

void FunctionDefinition():
{}
{
	"def" <IDENTIFIER> ArgumentList() [":" Type() ] "{"
		FunctionBody() 
	"}"
}

void ArgumentList():
{}
{
	"(" [Argument() ( "," Argument())*] ")"
}

void Argument():
{}
{
	<IDENTIFIER> ":" Type()
}

void FunctionBody():
{}
{
	StatementList()
}

void StatementList():
{}
{
	(Statement())*
}

void Statement():
{}
{
	VariableDeclaration() |
	AssignmentOrExpression() |	
	Block() |
	IfBlock() |
	WhileBlock() |
	ForBlock()
}

void VariableDeclaration():
{}
{
	"val" <IDENTIFIER> ":" Type() |
	"var" <IDENTIFIER> ":" Type()
}

void Block():
{}
{
	"{" StatementList() "}"
}

void IfBlock():
{}
{
	"if" "(" ")" Block()
	("elif" "(" ")" Block())*
	[ "else" Block() ]
}

void WhileBlock():
{}
{
	"while" "(" ")" Block()
}

void ForBlock():
{}
{
	"for" "(" ")" Block()
}

void Type():
{}
{
	"bool" |
	"u8" | "u16" | "u32" | "u64" |
	"i8" | "i16" | "i32" | "i64" |
	"f32" | "f64" |
	<IDENTIFIER> 
}

void AssignmentOrExpression():
{}
{
	Expression() [ "=" Expression() ]
}

void Expression():
{}
{	
	LogicalExpression()
}

void LogicalExpression():
{}
{
	BitExpression() ( ("||" | "&&") BitExpression() )*
}

void BitExpression():
{}
{
	EqualityExpression() ( ("&" | "|" | "^") EqualityExpression())* 
}

void EqualityExpression():
{}
{
	RelationalExpression() (("==" | "!=") RelationalExpression())*
}

void RelationalExpression():
{}
{
	ShiftExpression() (("<" | ">" | "<=" | ">=") ShiftExpression())*
}

void ShiftExpression():
{}
{
	AddExpression() (("<<" | ">>") AddExpression())*
}

void AddExpression():
{}
{
	MultExpression() (("+" | "-") MultExpression())*
}

void MultExpression():
{}
{
	PostfixExpression() (("*" | "/" | "%") PostfixExpression())*
}

void PostfixExpression():
{}
{
	PrimaryExpression() (
		"[" Expression() "]" |
		"(" [ ArgumentExpressionList() ] ")" |
		"." <IDENTIFIER> )*
}

void ArgumentExpressionList():
{}
{
	Expression() ("," Expression())*
}

void PrimaryExpression():
{}
{
	<IDENTIFIER> |
	Constant() |
	"(" Expression() ")"
}

void Constant():
{}
{
	<INTEGER_LITERAL> |
	<FLOATING_POINT_LITERAL> |
	<CHARACTER_LITERAL> |
	<STRING_LITERAL>
}